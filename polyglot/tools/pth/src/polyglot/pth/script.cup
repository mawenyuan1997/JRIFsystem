package polyglot.pth;

import java_cup.runtime.ComplexSymbolFactory;

import java.io.*;
import java.util.*;

parser code {:
    protected TestFactory tf;

    public Grm(TestFactory tf, String filename) {
        this(tf, new File(filename));
    }

    public Grm(TestFactory tf, File file) {
        this(tf, lexer(tf, file));
    }

    public Grm(TestFactory tf, Lexer l) {
        this(l, new ComplexSymbolFactory());
        this.tf = tf;
    }

    protected static Lexer lexer(TestFactory tf, File file) {
        try {
            return tf.Lexer(new FileReader(file));
        }
        catch (IOException e) {
            System.out.println("open script file (" + file.getName() +") error: " + e);
            return tf.Lexer(new InputStreamReader(System.in));
        }
    }

    public static int findErrorKind(String s) {
      s = s.toLowerCase();
      for (int i = 0; i < 10; i++) {
          String t = polyglot.util.ErrorInfo.getErrorString(i);
          if (t.toLowerCase().startsWith(s))
            return i;
      }
      return -1;      
    }
:}


terminal String IDENT;
terminal String STRING_LITERAL;
terminal LPAREN, RPAREN;
terminal LBRACK, RBRACK;
terminal LBRACE, RBRACE;
terminal SEMICOLON, COMMA;
terminal PLUS;

terminal BUILD;

non terminal List<Test> goal;
non terminal List<Test> command_list_opt;
non terminal List<Test> command_list;
non terminal Test command;
non terminal SourceFileTestCollection compilertest;
non terminal String testname_opt;
non terminal String testpath_opt;
non terminal String command_line_args_opt;
non terminal List<SourceFileTest> filetest_list_opt;
non terminal List<SourceFileTest> filetest_list;
non terminal SourceFileTest filetest;
non terminal List<List<String>> compilation_unit_list;
non terminal List<String> filename_list;
non terminal String description_opt;
non terminal List<ExpectedFailure> failure_set_opt;
non terminal List<ExpectedFailure> failure_set;
non terminal String sentence;
non terminal ExpectedFailure failure;

start with goal;

goal ::=
        command_list_opt:a
            {: RESULT = a; :}
    ;

command_list_opt ::=
        {: RESULT = Collections.emptyList(); :}
    |   command_list:a {: RESULT = a; :}
    ;

command_list ::=
        command:a
            {: 
               List<Test> l = new LinkedList<>();
               l.add(a);
               RESULT = l;
            :}
    |   command_list:a command:b
            {: a.add(b); RESULT = a; :}
    ;

command ::=
        BUILD
            {: 
               RESULT = tf.BuildTest();
            :}
    |   compilertest:a
            {: 
               RESULT = a;
            :}
    ;

compilertest ::=
        IDENT:a testname_opt:b testpath_opt:c command_line_args_opt:d LBRACE
                filetest_list_opt:e RBRACE
            {: 
               RESULT = tf.SourceFileTestCollection(a, b, c, d, e);
            :}
    ;

testname_opt ::=
        LPAREN sentence:a RPAREN
            {: RESULT = a; :}
    | {: :}
    ;

testpath_opt ::=
        LBRACK sentence:a RBRACK
            {: RESULT = a; :}
    | {: :}
    ;

command_line_args_opt ::=
        sentence:a
            {: RESULT = a; :}
    | {: :}
    ;

filetest_list_opt ::=
        {: RESULT = new LinkedList<>(); :}
    |   filetest_list:a {: RESULT = a; :}
    ;

filetest_list ::=
        filetest:a SEMICOLON
            {: List<SourceFileTest> list = new LinkedList<>();
               list.add(a);
               RESULT = list;
            :}
    |   filetest:a SEMICOLON filetest_list:b
            {: b.add(0, a); RESULT = b; :}
    ;

filetest ::=
            {: SourceFileTest sft =
                    tf.SourceFileTest(Collections.singletonList(
                        Collections.<String> emptyList()), Collections.<ExpectedFailure> emptyList());
               RESULT = sft; :}
    |   compilation_unit_list:a description_opt:b failure_set_opt:d
            {: SourceFileTest sft = tf.SourceFileTest(a, d);
               sft.setDescription(b);
               RESULT = sft; :}
    ;

compilation_unit_list ::=
		filename_list:a
			{: List<List<String>> list = new LinkedList<>();
			   list.add(a);
			   RESULT = list;
			:}
	|	compilation_unit_list:a COMMA filename_list:b
			{: a.add(b); RESULT = a; :}
	;

filename_list ::=
        IDENT:a
            {: List<String> list = new LinkedList<>();
               list.add(a);
               RESULT = list;
            :}
    |   filename_list:a IDENT:b
            {: a.add(b); RESULT = a; :}
    ;

description_opt ::=
            {: :}
    |   sentence:a
            {: RESULT = a; :}
    ;

sentence ::=
    STRING_LITERAL:a
		{:
			RESULT = a;
		:}
    |   sentence:a PLUS STRING_LITERAL:b
        {:
            RESULT = a + b; 
        :}
    ;

failure_set_opt ::=
            {: List<ExpectedFailure> l = new ArrayList<>();
               RESULT = l;
            :}
    | failure_set:a
            {: RESULT = a; :}
    ;
    
failure_set ::=
        failure:a
            {: List<ExpectedFailure> l = new ArrayList<>();
               l.add(a);
               RESULT = l;
            :}
    |   failure_set:a COMMA failure:b
            {: a.add(b); RESULT = a; :}
    ;

failure ::= 
         LPAREN IDENT:a RPAREN 
            {: 
              RESULT = new ExpectedFailure(Grm.findErrorKind(a));
             :}
    |    LPAREN RPAREN 
            {: 
              RESULT = new AnyExpectedFailure();
             :}
    |    LPAREN IDENT:a COMMA STRING_LITERAL:b RPAREN 
            {: 
              RESULT = new ExpectedFailure(Grm.findErrorKind(a), b);
             :}
    |    LPAREN STRING_LITERAL:b RPAREN 
            {: 
              RESULT = new ExpectedFailure(b);
             :}
    ;
